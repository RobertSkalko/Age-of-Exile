plugins {
    id 'fabric-loom' version '0.8-SNAPSHOT'
    id 'maven-publish'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
    maven { url 'https://raw.githubusercontent.com/Devan-Kerman/Devan-Repo/master' }
    maven { url "https://maven.theillusivec4.top" }
    maven { url 'https://dl.bintray.com/ladysnake/libs' }
    maven { url 'https://ladysnake.jfrog.io/artifactory/mods' }
    maven { url 'https://maven.terraformersmc.com/releases' }
    maven { url 'https://maven.blamejared.com' }
    maven { url "https://maven.shedaniel.me" }
    maven { url "https://jitpack.io" }
}

dependencies {
    // Base
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    optionally("net.fabricmc:fabric-loader:${project.loader_version}")
    modApi("net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}") {
        exclude(module: "fabric-dimensions")
        exclude(module: "fabric-biomes")
    }

    //API
    optionally("io.github.onyxstudios.Cardinal-Components-API:cardinal-components-base:${cardinal_version}")
    optionally("io.github.onyxstudios.Cardinal-Components-API:cardinal-components-entity:${cardinal_version}")
    optionally("io.github.onyxstudios.Cardinal-Components-API:cardinal-components-world:${cardinal_version}")
    optionally("io.github.onyxstudios.Cardinal-Components-API:cardinal-components-chunk:${cardinal_version}")
    optionally("me.sargunvohra.mcmods:autoconfig1u:${auto_config_version}")
    optionally("com.terraformersmc:modmenu:${mod_menu_version}")
    optionally("top.theillusivec4.curios:curios-fabric:${curios_version}")
    optionally("me.shedaniel:RoughlyEnoughItems:${rei_version}")

    // add back whenever i need to test the patchouli guide book
    //optionally("vazkii.patchouli:Patchouli:${patchouli_version}")


    // .dev .jar doesn't include included deps, so I have to add them for in IDE to work
    implementation files("D:/MinecraftModding/library_of_exile/build/libs/lib.jar")
    implementation files("World-Of-Exile-1.16.5-1.2.4-dev.jar")
}


remapJar {
    include file("World-Of-Exile-1.16.5-1.2.4.jar")
}

tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"

    // The Minecraft launcher currently installs Java 8 for users, so your mod probably wants to target Java 8 too
    // JDK 9 introduced a new way of specifying this that will make sure no newer classes or methods are used.
    // We'll use that if it's available, but otherwise we'll use the older option.
    def targetVersion = 8
    if (JavaVersion.current().isJava9Compatible()) {
        it.options.release = targetVersion
    }
}


def optionally(str) {
    runtime(str)
    dependencies.modCompileOnly(str) {
        exclude(group: "net.fabricmc")
        exclude(group: "net.fabricmc.fabric-api")
        exclude(group: "io.github.prospector")
        exclude(module: "config-2")
    }
}

def runtime(str) {
    dependencies.modRuntime(str) {
        exclude(group: "net.fabricmc")
        exclude(group: "net.fabricmc.fabric-api")
        exclude(group: "io.github.prospector")
        exclude(module: "config-2")
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

sourceSets {
    main {
        resources {
            srcDirs += project.file("src/generated/resources")
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

jar {
    from "LICENSE"
}


// CURSEFORGE STUFF

curseforge {

    def curseProp = new Properties()
    File secretPropsFile = file("key.properties")
    curseProp.load(secretPropsFile.newInputStream())

    project {
        apiKey = curseProp.getProperty('curseKey')
        id = "${curse_id}"
        releaseType = "${curse_release}"
        changelogType = 'markdown'
        changelog = file('CHANGELOG.md')
        addGameVersion("${project.minecraft_version}")
        addGameVersion("Fabric")
        addGameVersion("Java 8")

        relations {
            requiredDependency 'fabric-api'
            requiredDependency 'library-of-exile'
            requiredDependency 'curios-fabric'
        }
        mainArtifact(remapJar)

        afterEvaluate {
            uploadTask.dependsOn("remapJar")
        }
    }
    options {
        forgeGradleIntegration = false
    }
}

